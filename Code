pragma solidity ^0.4.0;

//Creating a smart contract for Smart Container violations

contract SmartContainer {
    
    //Participating Actors with Ethereum Addresses
    address public container;
    address public manufacturer;
    address vaccination_center; // only authorized vaccination center is allowed
    mapping(address => bool) distributor; // only authorized distributors are allowed
    string container_content; 
    enum containerStatus {NotReady, ReadyforDelivery, StartDelivery, onTrack, EndDelivery, ContainerReceived, Violated}
    containerStatus public state;
    uint startTime;
    enum violationType { None, Temp, Open, Light, Route}
    violationType public violation;
    int temperature; // temperature tracking
    int open;
    int track; // 1 if on track, 0 if not 
    int light;
    
    
    
//Tracking Events
event ContainerOwnership (address previousowner, address newowner); // Manufacturer announces container is created
event ContainerReadyForDelivery (address manufacturer); //Manufacturer announces container is ready for delivery 
event DeliveryStart (address distributor); //Distributor announces that start of the delivery process
event DeliveryEnd(address distributor); // Distributor announces the end of the delivery 
event ContainerReception(address vaccination_center); //Vaccination center announces the receiption of the container 

//Violations Events
event TemperatureViolation( int v); 
event ContainerOpening( int v);
event OffTrack( int v);
event LightViolation ( int v);
event ErrorNoValidViolation();


//Defining Modifiers 

modifier onlyManufacturer() {
    require(manufacturer == msg.sender, "The sender is not eligible to run this function");
    _;
}

modifier onlyDistributor () {
    require(distributor[msg.sender], "The sender is not eligible to run this function");
    _;
}

modifier onlyVaccinationCenter() {
    require(vaccination_center == msg.sender, "The sender is not eligible to run this function");
    _;
}

modifier onlyContainer() {
    require(container == msg.sender, "The sender is not eligible to run this function");
    _;
}

//Creating the contract constructor

constructor() public payable{
    manufacturer = msg.sender;
    startTime = block.timestamp;
    container = 0x4B20993Bc481177ec7E8f571ceCaE8A9e22C02db;
    vaccination_center = 0x78731D3Ca6b7E34aC0F824c42a7cC18A495cabaB;
    container_content = "This container contains X amount of Vaccine doses.";
    state = containerStatus.NotReady;//NotReady
    emit ContainerOwnership(address(0), manufacturer);
    
}

// View Container manufacturer
    function containerManufacturer() public view returns (address _currentOwner){
        return manufacturer;
    }
    

//Authorization Function

     function distributorRegistration (address user) public onlyManufacturer { //manufacturer adds only authorized distributors
        distributor[user]=true;
    } 
    
    
    //Smart Container Tracking

    function CreateContainer() public onlyManufacturer{
        require(state == containerStatus.NotReady, "The smart container has already been created");
        state = containerStatus.ReadyforDelivery;
        emit ContainerReadyForDelivery(msg.sender);
    }
    
    function StartDelivery() public onlyDistributor{
        require(state == containerStatus.ReadyforDelivery, "Can't start delivery before creating the container");
        state = containerStatus.onTrack;
        emit DeliveryStart(msg.sender);
    }
    
    function EndDelivery() public onlyDistributor{
        require(state == containerStatus.onTrack, "Can't end delivery before announcing the start of it");
        state = containerStatus.EndDelivery;
        emit DeliveryEnd(msg.sender);
        
    }
    
    function ReceiveContainer() public onlyVaccinationCenter{
        require(state == containerStatus.EndDelivery, "Can't receive the container before announcing the end of the");
        state = containerStatus.ContainerReceived;
        emit ContainerReception(msg.sender);
    }
    
    
    //Violations Monitoring 
    
    function violationOccurrence(violationType v, int value) public onlyContainer{
        require(state == containerStatus.onTrack, "The container is not being delivered"); // Monitoring starts when the container is on track
        
        state = containerStatus.Violated;
        if(v == violationType.Temp){
             
            emit TemperatureViolation( value);
        }
        else if (v == violationType.Open){
            //either 1 or 0
            emit ContainerOpening ( value);
        }
        else if (v == violationType.Route){
           
            emit OffTrack(  value);
        }
        else if (v == violationType.Light){
           
            emit LightViolation( value);
        }
        else
            emit ErrorNoValidViolation();
    }
    


}
